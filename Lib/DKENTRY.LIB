/*** BeginHeader */
#ifndef __DKENTRY_LIB
#define __DKENTRY_LIB
/*** EndHeader */

/*** BeginHeader dkPrologCount, dkOSRunning, dkContextSwitch, dkLastTCB,
				     dkDbgTCB */

extern char dkPrologCount;
extern char dkOSRunning;
extern char dkContextSwitch;
extern void* dkLastTCB;
extern void* dkDbgTCB;
/*** EndHeader */

char dkPrologCount;
char dkOSRunning;
char dkContextSwitch;
void* dkLastTCB;
void* dkDbgTCB;

/*** BeginHeader dkExit */
xmem void dkExit(void);
/*** EndHeader */

#asm xmem
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
dkExit::
#ifdef MCOS
	ld		a,(dkStatusFlags)
	bit	DKF_STAT_CONTEXTSWITCH,a
	jr		z,__dk_uedone
	ld		hl,(OSTCBCur)
	ld		(dkLastTCB),hl
__dk_uedone:
#endif
   lret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#endasm

/*** BeginHeader dkSingleStepExit */
xmem void dkSingleStepExit(void);
/*** EndHeader */

#asm xmem
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
dkSingleStepExit::
#ifdef MCOS
	ld		hl,(OSTCBCur)
	bool	hl
	jr		z,__dk_csse_done		;exit if os is not yet running
	ld		hl,(OSTCBCur)			;set current TCB as tcb being debugged
	ld		(dkDbgTCB),hl

__dk_csse_done:
#endif
	lret
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#endasm

/*** BeginHeader dkProlog */
root void dkProlog(void);
/*** EndHeader */

#asm root
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; dkProlog
; this routine is called on each entry to the debug kernel
dkProlog::
	push	ip
	ipset	1

	push	af
	push	hl

#ifdef MCOS
   ld		a,(dkPrologCount)
	or		a
	jr		nz, __dk_prolog_exit

	ld		hl,(OSRunning)			; disable MCOS scheduling if MCOS active
	ld		a,l
	or		l
	jr		z,__dk_prolog_exit

	ld		a,l
	ld		(dkOSRunning),a
	xor	a
	ld		(OSRunning),a

__dk_prolog_exit:
	ld		a,(dkPrologCount)
	inc	a
	ld		(dkPrologCount),a
#else
	ld		hl,(FSP)
	ld		a,h
	or		l
	jr		z,__dkprolog_done

	ld		(DBGFSP),hl

	bool	hl
	ld		l,h
	ld		(FSP),hl

__dkprolog_done:
#endif
	pop	hl
	pop	af
	pop	ip
	ret
#endasm

/*** BeginHeader dkEpilog */
root void dkEpilog(void);
/*** EndHeader */

#asm
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; dkEpilog
; this routine is called on each exit from the debug kernel
dkEpilog::
	push	ip
	ipset	1

	push	af

#ifdef MCOS
	ld		a,(dkPrologCount)
	dec	a
	ld		(dkPrologCount),a

	or		a
	jr		nz,__dk_epilog_exit

	ld		a,(dkOSRunning)			;restore MCOS active status
	ld		(OSRunning),a
	xor	a
	ld		(dkOSRunning),a

__dk_epilog_exit:
#else
	push	hl
	ld		hl,(DBGFSP)
	ld		(FSP),hl
	bool	hl
	ld		l,h
	ld		(DBGFSP),hl

	pop	hl
#endif
	pop	af
	pop	ip
	ret
#endasm

/*** BeginHeader dkDetectContextSwitch */
xmem void dkDetectContextSwitch(void);
/*** EndHeader */
#asm xmem
dkDetectContextSwitch::
	ld		hl,(OSTCBCur)
	ld		a,l
	or		h
	jr		z,__dk_dcsdone

	ld		hl,(dkLastTCB)
	ld		a,l
	or		h
	jr		nz,__dk_dcsosstarted
	ld		(dkLastTCB),hl		; if os just started, init OSDbgTCB

__dk_dcsosstarted:

	ld		hl,(OSTCBCur)
	ld		de,(dkLastTCB)
	xor	a											; clear carry
	sbc	hl,de
	jr		z,__dk_dcsdone


	ld		a,(dkStepFlags)
	bit	DKF_STEP_OVER,a
	jr		nz,__dk_dcsdone
	add	sp,2
	ld		a,TC_DEBUG_SINGLESTEP
	ld		(dkRequest),a
	ld		hl,dkStatusFlags
	set	DKF_STAT_CONTEXTSWITCH,(hl)
	ljp	dkSingleStep				; bypass dkCore and treat this as a
	                              ; source level step. Context switch occurred while
	                              ; single stepping in disassembled code.
__dk_dcsdone:
	lret
#endasm

/*** BeginHeader dkHandleRST18 */
root void dkHandleRST18(void);
/*** EndHeader */

#ifdef DK_ENABLE_ASMSINGLESTEP

#asm root
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Disassembled code single step handler.  After an instruction
; executes in RAM, execution ends up here.
dkHandleRST18::
	push	ip
	ipset	1
	call	dkProlog
	pop	ip
#ifdef MCOS
	call	dkCheckCurrentTask
#endif
	call	dkSaveContext				; save context of user program
#if _RK_FIXED_VECTORS
	add	sp,5
#else
	add	sp,2							; adjust stack pointer to remove return address
#endif
	call 	dkSavepc
#ifdef MCOS
   call	dkDetectContextSwitch
#endif
	; if next instruction to execute is an rst 20 or 28, jump straight
	; to the asm single step handler, and skip the instruction
	ld		iy,(dkRegisters+__dkregisters+_pc)
	ld		a,(dkRegisters+__dkregisters+_xpc)
	lcall	__dk_ldp_LogToPhyCode
	ld		a, l
	cp		DK_RST20
	jr		nz,.dk_notrst20
	ljp	dkAsmSingleStep
.dk_notrst20:
	cp		DK_RST28
	jr		nz,.dk_notrst28
	ljp	dkAsmSingleStep
.dk_notrst28:
#if _USER
	; user program compile, must go through syscall interface
	ld		hl,0x0001
	push	hl
	call	_sys_dk_nestinterrupts
	add	sp,2
#else
	ld		iy,TCState					; disable nesting of interrupts during tc callbacks
	ld		a,1
	TC_STOR(dont_nest_interrupts,a)
#endif
	ljp	dkCore
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#endasm

#else // assembly level single stepping not enabled

#asm
dkHandleRST18::
	ld		hl,DTP_ASMSINGLESTEP
	jp		dkUnsupportedCommand
#endasm

#endif

/*** BeginHeader dkCheckCurrentTask */
root void dkCheckCurrentTask(void);
/*** EndHeader */
#asm root
dkCheckCurrentTask::
	push	af
	push	hl
	push	de
	ld		hl,(OSTCBCur)
	bool	hl
	jr		z,__dkcct_done		; if OSTCBCur = 0, os not yet running
	ld		hl,(dkDbgTCB)
	bool	hl
	jr		nz,__dk_cctosstarted
	ld		hl,(OSTCBCur)
	ld		(dkDbgTCB),hl		; if os just started, init OSDbgTCB

__dk_cctosstarted:
	ld		a,(dkStepFlags)
	bit	DKF_STEP_INTO,a
	jr		nz,__dkcct_done		; if stepping into, must enter dk

	ld		a,(dkRequest)			; if stopping, stop regardless of running task
	cp		TC_DEBUG_STOPPROGRAM
	jr		z,__dkcct_done


__dk_cctnotstop:
	ld		hl,(OSTCBCur)
	ex		de,hl
	xor	a						; clear carry
	ld		hl,(dkDbgTCB)		; TCB of task currently being debugged
	sbc	hl,de
	jr		z,__dkcct_done		; (z set at last sbc) enter dk if this task is being debugged

									; once this is actually implemented, should check to see if
									; STOPPROGRAM is the current command. If so, even if the
									; current task is not the one we are interested in, the debug
									; kernel should be entered so that the program can be stopped.

#if (DK_ENABLE_TRACING == 1)
__dk_tracesendcont:
	ld		a,(dkSendFlags)					; get send flags into a
	bit	DKF_SEND_TRACE,a				   ; check if watches should be sent
	jr		z,__dkcct_notrace
   ld    hl,dkDebugTraceMsg
   lcall dkSendTrace
	ld		hl,dkTraceFlags
   bit	DKF_TRACE_FENTRY,(hl)
   res	DKF_TRACE_FENTRY,(hl)
   res	DKF_TRACE_FEXIT,(hl)
   jr		nz,__dk_tracesendcont    ; now repeat Entry trace with Execute or MACRO
   res	DKF_TRACE_RST,(hl)
__dkcct_notrace:
#endif
	pop	de
	pop	hl
	pop	af
	add	sp,2
	jp		dkEpilog

__dkcct_done:
	pop	de
	pop	hl
	pop	af
	ret
#endasm


/*** BeginHeader dkHandleRST28 */
root void dkHandleRST28(void);
/*** EndHeader */

#asm
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Source level single step.  An rst 28 is
; placed before every series of instructions making up a single
; c statement in non-nodebug code.
dkHandleRST28::
	push	ip
	ipset	1
	call 	dkProlog
	pop	ip

#ifdef MCOS
	call	dkCheckCurrentTask
#endif

	push	af
   call	dkCheckEntry
	jp		z,.dk_hr28enter
#if _RK_FIXED_VECTORS
	pop	af
	lret
#else
	pop	af
	ret
#endif

.dk_hr28enter:
	pop	af

#if (DK_ENABLE_TRACING == 1)
	lcall	dkSetTraceRST28
#endif

	call 	dkSaveContext
	call	dkSavepcrst2028

#if _RK_FIXED_VECTORS
	add	sp,5
#else
	add	sp,2
#endif
#if (DK_ENABLE_ASMSINGLESTEP == 1)
	call dkSetReturnAddress
#endif
	ljp	dkCore
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#endasm

/*** BeginHeader */
#endif
/*** EndHeader */